
=== Generalities

In this section we will provide the definitions you may need in order to fully understand the syntax explanations in the rest of the chapter.
We will also provide the standard types used in the visual kopi framework. 

==== Syntax terms

During this chapter, you will have many *Syntax* sections containing different keywords and special characters :


 * *'SimpleName and QualifiedName'*

When setting up your own application, you will often have to enter a name in order to define either a field, a block, a page or a form. You will then have to make use of either a simple or a qualified name.

A SimpleName consists of a character row in which neither spaces nor points are permitted.

*Example*
[source,java]
-----------------
StudentGroup2
-----------------

A QualifiedName is a row of characters in which points are allowed.

*Example*
[source,java]
------------------
StudentGroup2.ViennaUniversity
------------------

 * *'Parameters'*

You can also find parameters with types in the syntax blocks; 

*Example*
[source,java]
----
Integer x
String name
Fixed y
----

 * *'Special Characters'*
 
These characters may be found ins syntax block and defines the rules of the syntax:

[ ]  : if an element in put between [ ] that means it's optional. +
*    : if an element or expression is followed by *, then it can be inserted zero or more times +
+    : if an element or expression is followed by +, then this element need to be inserted at least one time, or more +
|    : means Or +
" "  : if and element or expression is put between " ", then it's a keyword of the language. 

*Example*
----
ItemDefinition  : "FORM" String formTitle ["IS" QualifiedName]
                  ["IMPLEMENTS" QualfiedName [,QualifiedName]* ] 
----

==== Predefined Field Types 

Every form or report field In visual kopi have field type that can be one of the 12 ready to use predefined types : 

[source,java]
----
- FIXED     // used to insert fixed values
- IMAGE      // used to insert images
- INTEGER    // used to insert integer values
- STRING     // used to insert string values
- TEXT       // used to insert text values
- DATE       // used to insert a date value
- BOOL       // used to insert a true or false value 
- COLOR      // used to set the fields color
- MONTH      // used to insert years months value
- TIME       // used to insert a hours:minutes time value
- TIMESTAMP  // used to insert a timestamp value
- WEEK       // used to insert years weeks  value
----

===== The STRING Field Type

A STRING is used to enter characters which can be either letters, numbers or both. The width has always to be given. Moreover, you can optionaly indicate how many rows it will contain and how many will finally be displayed on the form. If these optional arguments are used, you have to indicate the carraige return method by specifying either the FIXED ON or the FIXED OFF option to avoid compilation errors. +
There are also three other options you can use in order to modify the String's case : 

- CONVERT NAME  (Converts the first letter of each word to capital letter)
- CONVERT UPPER (Convert the whole text to capital letters)
- CONVERT LOWER (Converts the whole text to normal letters)

*Syntax:*
 	
[source,java]
----
StringType   : "STRING" (Integer width ,Integer height ,Integer visible)
               [FixOption]
               [StringFormat] 
              
FixOption    : "FIXED" "ON"
               "FIXED" "OFF"             

StringFormat : "CONVERT" "UPPER"
               "CONVERT" "LOWER"
               "CONVERT" "NAME" 
----

*Example*
 	
[source,java]
----
STRING (40,10,4)
FIXED ON
CONVERT UPPER
----

In this example, the text inserted will contain up to 40 characters and up to 10 rows. However, only the first 4 rows will be displayed on the form. Moreover, All the letters in the text will be converted to capital letters.

===== The TEXT Field Type

A Text and a String are similar apart from the fact that in a text, two parameters have always to be given: namely the width and the height of the field whereas you only need to determine the width in a string.

*Syntax:*
 	
[source,java]
----
TextType      : "TEXT" (Integer width ,Integer height ,Integer visible)
                [FixOption]

[FixOption]   : "FIXED" "ON"
                "FIXED" "OFF"
----

For example, you can write STRING (40, 10, 4) or STRING (40) but you have to write TEXT (40, 10) or TEXT (40, 10, 4).

===== The IMAGE Field Type

This field type is used to insert an illustration or a picture. When introducing an IMAGE, you have to determine its width and height. These values have to be integers and are measured in pixel. In this case, the two attributes are compulsory.The field will look like a file chooser that lets you choose and image file to show in the field.

*Syntax:*
 	
[source,java]
----
ImageType : "IMAGE" (Integer width , Integer height)
----
*Example:*
 	
[source,java]
----
IMAGE(20,10) 
----
In this field, the image will have a width of 20 pixel and a height of 10 pixel.

===== The FIXED Field Type 

A FIXED is used to insert numbers, integers, fixed point numbers. Fraction numbers are entered with the field type FRACTION. 
The maximal width has to be determined for all them. In case of a fixed point number FIXNUM, the maximal scale i.e the number of characters standing after the comma has also to be defined. 
Also the comma has to be counted as a character. Only the width is to be defined in a FRACTION. 
You can also set the minimum and the maximum values for the FIXNUM field with the optional parameters MINVAL and MAXVAL. 


*Syntax:*
 	
[source,java]
----
FixedType   :  "FIXNUM" (Integer width ,Integer scale)
               ["MINVAL" Fixed minValue]
               ["MAXVAL" Fixed maxValue]   

FractionType : "FRACTION" (Integer width)
               ["MINVAL" Fixed minValue]
               ["MAXVAL" Fixed maxValue]   
               
---- 


*Example:*
 	
[source,java]
----
FIXED(4,2)      // for 1,25
  MINVAL 0
  MAXVAL 3.0 
    
 
FRACTION(7)    //  for 1 35/64 
  MINVAL 0
  MAXVAL 25
----

===== The INTEGER Field Type

Integer field type is  LONG is used to insert integers. Only the text width is to be defined. The MINVAL and MAXVAL options are also available for this type.

*Syntax:*
 	
[source,java]
----
IntegerType:   "LONG" (Integer width)
               ["MINVAL" Integer minValue]
               ["MAXVAL" Integer maxValue]   
----

*Example*
 	
[source,java]
----
LONG(6)
  MINVAL 0
  MAXVAL 100 
----

==== Code Field Types

In addition to the predefined field types already available in visual kopi, you can define more specific types which are the ENUM type and the CODE type. Unlike the predefined field types, these types have to be set in the Type definition type of the form before you can use them in the fields.

===== The ENUM Type

The ENUM type means enumeration or listing. An enumeration definition is made up of one or several strings and only the strings you have listed can be entered in the field.

*Syntax:*
 	
[source,java]
----
EnumType:   "ENUM" (EnumList)

EnumList:   String member [,EnumList]
----

*Example*
 	
[source,java]
----
ENUM ("X-Small","Small","Medium", *  Large", *  X-Large" )
----

===== The CODE types 

There are four different sorts of CODE :
* The CODE BOOL
* The CODE LONG
* The CODE FIXED 
* The CODE STRING

These types enable you to have a list of item-value pairs, the items will be displayed in the field and the values will be assigned instead. 

 *  *The Type CODE BOOL*

In a CODE BOOL or BOOLEAN you have to assign a Boolean value to the item you have entered. Boolean values are values like "True" or "False" and "Yes" or "No".

*Syntax:*
 	
[source,java]
----
CodeBooleanType : "CODE" "BOOL" "IS"
                  CodeBooleanList  
                  "END" "CODE"

CodeBooleanList : CodeBoolean  [CodeBooleanList]

CodeBoolean     : String code "=" Boolean value
----

*Example*
 	
[source,java]
----
CODE BOOL IS
  "married" = true
  "single"  = false
END CODE
----

 * *The Type CODE LONG*

In a CODE LONG, you assign to each String item you have entered a LONG value.

*Syntax:*
 	
[source,java]
----
CodeIntegerType : "CODE" "LONG" "IS" 
                  CodeIntegerList
                  "END" "CODE"
                  
CodeIntegerList : CodeInteger  [CodeIntegerList]

CodeInteger     : String code "=" Integer value 
----

*Example*
 	
[source,java]
----
CODE LONG IS
  "Monday" = 1
  "Tuesday" = 2
  "Wednesday" = 3
  "Thursday" = 4
  "Friday" = 5
  "Saturday" = 6
  "Sunday" = 7
END CODE
----

 * *The Type CODE FIXED*

In a CODE FIXED, each item you have entered will get a FIXED value, i.e integers, fixed point numbers and fraction numbers.

*Syntax:*
 	
[source,java]
----
CodeFixedType : "CODE" "FIXED" "IS"
                [CodeFixedList]
                "END" "CODE"

CodeFixedList : CodeFixed  [CodeFixedList]
  
CodeFixed     : String code "=" Fixed value
----
*Example*
 	
[source,java]
----
CODE FIXED IS
  "piece" = 1.00
  "per cent" = 0.01
END CODE
----

 * *The Type CODE STRING*

In a CODE STRING, each item you have entered will get a STRING value, this can be useful for shortcut of long strings for example.

*Syntax:*
 	
[source,java]
----
CodeStringType : "CODE" "STRING" "IS"
                 [CodeStringType]
                 "END" "CODE"

CodeStringType : CodeString  [CodeStringType]
  
CodeString     : String code "=" String value
----
*Example*
 	
[source,java]
----
CODE STRING IS
  "JDK" = "Java Development Kit"
  "JRE" = "Java Runtime Environment"
END CODE
----

==== The LIST command

Once you have defined a field type, you can make use of the LIST command in order to refer the user to a list or a table in the database which will help him when filling in the form in question. +
If you refer a field type to a certain table, the field will get an icon on which you can click in order to retrieve this table. As this command connects the user with a certain table, you have to enumerate all columns of the table which information could be helpful for the user. +
In so doing, you will then have to enter at least one column. The information contained in the first element of the list must have the same standard type as the defined type as it is the one which will be entered in the field in question. In addition, you may choose to refer to an existing form (that should extend the VDictionaryForm class) using either the NEW command to get a button on the bottom of the list allowing you to get to the referred form, or the ACCESS command to bypass the list and get directly to the referred form.


*Syntax:*
 	
[source,java]
----

List        : "LIST" String tableName ["NEW" | "ACESS" QualifiedName ] 
              "IS" ListColumns 
              "END" "LIST"     

ListColumns : ListColumn  [ListColumns]

ListColumn  : [String field "="] SimpleName ":" Predefined Field Type   

----

This command is usually used when defining a new type, here is an example where we define the Lecturer Type as a STRING(8) value from the Symbol field of the "Lecturer" table on the database, when clicking on a field of Type Lecturer, you will have a list with three columns (Symbol, Surname and Lesson) retrieved from the "Lecturer" table. Selecting a row from this list will put the symbol value in the field.

*Example*
 	
[source,java]
----
TYPE Lecturer IS
  STRING (8)
  CONVERT UPPER

  LIST "Lecturer" IS
    "Symbol" = Symbol : STRING (8)
    "Name" = Name : STRING (40)
    "Surname" = Surname : STRING (40)
    "Lesson" = Lesson : STRING (20)
  END LIST
...
END TYPE
----

===== The SELECT command

As you had the possibility to call up a list or a table from the database with the option LIST, you now can make Kopi sort out information from a whole list or a table which the option SELECT and this, according to criteria you have to define. Letâ€™s say you want to view the degrees which can be achieved in a certain year. For this, you have to use the SELECT command as you see in the following rows:

*Example*
 	
[source,java]
----
TYPE CurrentDegree (Integer year) IS
     STRING (8)
       CONVERT UPPER
  LIST{(
        SELECT Symbol, Description
        FROM   Degree
        WHERE Year = : (year)
        )} IS

        "Symbol" = Symbol : STRING (8)
        "Description" = Description : STRING (40)
  END LIST
END TYPE
----
"Degree" is the database table to which we have to access in order to select the information. The command SELECT is used in order to enter the columns in which Kopi has to make its research. After FROM, you have to enter the table from which these columns are to be selected and after WHERE, you have to enter the conditions according to which this selection has to be carried out.

The sign "=" means the value of the data delivered after the selection must correspond to the conditions you have entered.
The sign ":" inserts one Java expression. After this Java expression, you can insert a SQL expression again. (See JLS 15.27)
Finally, you have to define for each of them the field type. 

As a result of your selection, you will then have a table with two columns which will contain the different sorts of degrees achievable in the year you have entered.

