
=== Visual Kopi Reports

Visual Kopi allows you to create dynamic reports. These are  files with the ".vr" extension that have a very simple structure, in fact, all you have to do to create such reports
is to list the definitions of all the fields you need, then you will have to write a constructor that will load data into these fields.
The so created reports will have dynamic functions such as sorting and group making, you will also be able to print it or export to different file formats.You can even add other fields
to the report after it's generated, do more calculus, customize the report columns...
The Visual Kopi Reports are generated from Visual Kopi form files with the ".vf" extension by different methods that we will see in this chapter, along with the process of making a
dynamic report.

==== Creating Report Fields

As we said in the introduction, the report structure is based on report fields, these fields begin with FIELD  and a SimpleName and ends with END FIELD and have the following syntax :

*Syntax*
[source,java]
----
ReportFieldDef: "FIELD" [Integer MultiField] SimpleName [FieldLabel]
                [HelpText] FieldType [FieldAlign] [FieldOptions]
                [GroupCommand] [FieldCommands] [FieldTriggers]
                "END" "FIELD"
GroupCommands : "GROUP" SimpleName
----

===== Single Field and Multiple Fields

When you define a news report field, you have the choice to make it multiple by adding an integer (i) just after the FIELD command, doing so will create i fields.
The generated fields will have the SimpleName indicated after the FIELD command plus a number from 1 to i.

*Example*
[source,java]
----
FIELD(3) Name
  ...
END FIELD
----
In this example we have created 3 fields (Name1, Name2 and Name3).

=====  Report Field Label

The field label is the name that will be shown in the dynamic report, you can define a label by entering the LABEL command followed by String separated with a comma. If you omit the Strings after the LABEL command, the field 
will have its SimpleName as label. You can also specify NO LABEL if you want your field to have no label at all.
If you specify more than one String after the LABEL command, then if your field is Single it will have the first String as label, otherwise, if it's a multiple field each generated field will have a label 
from the Strings by order.

*Example*
[source,java]
----
FIELD Name
  LABEL "Person Name"
  ...
END FIELD


FIELD(3) LastName
  LABEL "Name1", "Name2", "Name3"
  ...
END FIELD


FIELD Age
  LABEL
  ...
END FIELD


FIELD ID
  NO LABEL
  ...
END FIELD
---- 

===== Report Field Help Text

HELP is used to insert an explanation text for the application user . This Help text will then be displayed when the user places the mouse on the report field.

*Example*
[source,java]
----
FIELD Name
  LABEL
  HELP *"The Person's First Name"
  ...
END FIELD
----

===== Report Field Type

The field type is a required entry, you may enter a predefined field type :

*Example*
[source,java]
----
FIELD Name
  LABEL
  STRING(10)
END FIELD


FIELD Age
  LONG(3)
END FIELD
----

You can also use self defined field types that you have previously defined in the type definition section of your report.

*Example*
[source,java]
----
FIELD objectList
  LABEL
  TYPE Objects
END FIELD
----

In this example, Objects is a type you should have defined previously in type definition section of the report using standard types, CODE types, ENUM types, LIST and SELECT command ...

===== Report Field Alignment

This command is used to define the localization of the field's content inside the field. There are three types of alignment.

* ALIGN RIGHT the value is displayed at the right inside the field
* ALIGN LEFT the value is displayed at the left inside the field
* ALIGN CENTER the value is centered in the field

*Example*
[source,java]
----
FIELD Name
  LABEL
  STRING(10)
  ALIGN CENTER
END FIELD
----

===== Report Field Options

There is actually only one option for the dynamic report fields in Kopi: the HIDDEN OPTION , if this option is used on a field, he will not be visible the the report.
This options have to be used on the last visible field of the report to avoid rendering bugs.

*Example*
[source,java]
----
FIELD Name
  LABEL
  STRING(10)
  HIDDEN
END FIELD
----

The field Name will not be visible on the report.

===== Report Field Group

You can create clickable groups in your report by using the keyword GROUP in you field followed by the field you want to be grouped by the actual field.

*Example*
[source,java]
----
FIELD Customers
  LABEL
  STRING(5)
END FIELD

FIELD Articles
  LABEL
  STRING(10)
END FIELD

FIELD InvoiceNum
  LABEL
  STRING(10)
  GROUP Articles
  GROUP Customers
END FIELD
----

In this report, you can click on the InvoiceNum field to group customers and articles.

===== Report Field Command

In report Fields, you can call commands with one of the following syntaxes :


[source,java]
----
"COMMAND" QualifiedName  
    
Example
COMMAND ExportCsv 
----

Or: 
   
[source,java]
---- 
"COMMAND" "ITEM" SimpleName "CALL" SimpleName "END" "COMMAND"

Example
COMMAND
  ITEM Export
  CALL function
END COMMAND
----

Or: 
   
[source,java]
----
"COMMAND" "ITEM" SimpleName "EXTERN" QualifiedName "END" "COMMAND"
    
Example    
COMMAND 
  ITEM Export
  EXTERN function
END COMMAND
----

Or:

[source,java]
----
"COMMAND" "ITEM" SimpleName "ACTION"
(VReportColulmn SimpleName) {Kopi-Java Code} 
"END" "COMMAND"
----

Or:

[source,java]
----
"COMMAND" "ITEM" SimpleName "ACTION" {Kopi-Java Code} 
"END" "COMMAND" 
----

===== Report Field Triggers

Report field triggers are special events that you can catch to execute other actions.

*Syntax*
[source,java]
----
Trigger      : EventList TrigerAction

EventList    : Event [,EventList]*

TrigerAction : SimpleName | "EXTERN" QualifiedName | {KOPI_JAVA code} |
               (VReportColulmn SimpleNAme){KOPI_JAVA code} 
----

Here is the two triggers available for report fields :

 * FORMAT  : Actually not available
 * COMPUTE : executed when the report is displayed and can be used to compute expressions on the report columns and show the result.

*Example*
[source,java]
----
FIELD Price
  LABEL
  FIXNUM(9, 4)
  COMPUTE  EXTERN ReportTriggers.sumFixed	
END FIELD
  
FIELD 
  LABEL
  STRING(10)
  FORMAT 
END FIELD  
----

==== Creating Reports

Visual Kopi Dynamic reports have a unique structure that begin with REPORT and ends with END REPORT as described by the following syntax :

*Syntax*
[source,java]
----
ReportDefinition  :[ReportLocalization] "REPORT" ReportTitle
                   [IS QualifiedName]Â 
                   ["IMPLEMENTS"  [,QualifiedName]*]
                   [ContextHeader] [ReportHelp] [ReportDefinitions]
                   "BEGIN" [ReportCommands] [ReportTriggers] (ReportFields)
                   [ContextFooter]
                   "END" "FIELD"

ReportTitle       :  String Title

ReportDefinitions : [MenuDefinition] [ActorDefinition] [TypeDefinition]
                    [CommandDefinition]
                    [InsertDefinition]
----

 
===== Report Localization

This is an optional step in which you may define the language of your forms menus and messages, the latter have to be defined in xml files.

*Example:*
[source,java]
----
LOCALE "en_EN"
-----

===== Report Title

Every Report have to begin with the keyword REPORT that you have to follow with a title.


*Example*
 	
[source,java]
----  
REPORT "Invoices"
  ...
END REPORT
----

===== Report Superclass And Interfaces

 * *Superclass:*

*Syntax:*
 	
[source,java]
----  
SuperClass:   "IS" QualifiedName 
----
Every Report is a class that may extend another Java class by using the optional keyword IS. Otherwise,  Kopi will automatically take over the java.lang.Object QualifiedName.

*Example:*
 	
[source,java]
----  
REPORT  "Orders Report" IS VReport
----
In other words, the class declaration you have just made specifies the direct superclass of the current class.

 * *Interfaces:*

You can also specify interfaces which the report may implement by using the IMPLEMENTS option. 

*Syntax*

[source,java]
----
"IMPLEMENTS" QualifiedName [,QualifiedName]*
----

*Example:*
 	
[source,java]
----  
REPORT "Orders Report" IS VReport
  IMPLEMENTS UReport
  ...
END REPORT
----

===== Report Header

*Syntax:*
 	
[source,java]
----  
ContextHeader:   { PackageDeclaration  ImportDeclarations  }

PackageDeclaration:   <As defined in JLS 7.4> 
ImportDeclarations:   <As defined in JLS 7.5> 
----
The package definition is contained in the header. It consists in defining where this report belongs to i.e which application, which firm or which person it is related to.

*Example*
 	
[source,java]
----  
REPORT "List of the Lecturers"
  {
  package at.dms.apps.timetable;
  }
  ...
END REPORT
----
In this example, the report belongs to the DMS firm and is a part of the application called "timetable". In addition to this Java package declaration, you can make a Java import declaration in order to import some classes from other packages. You can add an unlimited number of imports.

*Example*
 	
[source,java]
----  
REPORT "List of the Lecturers"
  {
    package at.dms.apps.tb;

    import java.util.Hashtable
    import java.util.Vector
  }
   ...
END REPORT
----


===== Report Help Text

After the report header, you can enter a help text for the report using the following syntax:

*Syntax*
[source,java]
----
HELP String helpText
----
Actually every report has a help menu that tries to describe the structure of the report by giving information about its commands and fields in a document, the help text will be on the top of this help menu document.

*Example*
[source,java]
----
REPORT "Orders Report"

  {
    package at.dms.apps.tb;

    import java.util.Hashtable
    import java.util.Vector
  }
  
HELP "This report lists purchase orders"
...

END REPORT

----

===== Report Menus Definition

Defining a menu means adding an entry to the menu bar in the top of the report, you can add actors to this menu later by specifying the menu name in the actor definition. In the menu definition, the LABEL is optional.

*Syntax:*
 	
[source,java]
----  
MenuDefinition:   "MENU" SimpleName ["LABEL" String label]   "END" "MENU" 
----

*Example*
 	
[source,java]
---- 
REPORT "List of the Lecturers"

  {
    package at.dms.apps.tb;
  }
  
  MENU newMenu
    LABEL "newMenu"
  END MENU
...
END REPORT
----

===== Report Actors Definition

An Actor is an item to be linked with a command, if its ICON is specified, it will appear in the icon_toolbar located under the menu bar, otherwise, it will only be accessible from the menu bar.
ICON,LABEL and KEY are optional, the KEY being the keyboard shortcut to assign to the actor.


*Syntax:*
 	
[source,java]
----  
ActorDefinition:  "ACTOR" SimpleName
                  "MENU"  SimpleName  
                  ["LABEL" String label]
                  "HELP"  String helpText
                  ["KEY"   String key]
                  ["ICON"  String icon]
                  "END" "ACTOR"
----

*Example*
 	
[source,java]
---- 
FORM "List of the Lecturers"

  {
    package at.dms.apps.tb;
  }
  
  MENU newMenu
    LABEL "newMenu"
  END MENU
  
  
  ACTOR printReport
    MENU  newMenu
    LABEL "Print"
    HELP  "Print the report" 
    ICON  "printerIcon"
  END ACTOR
...
END REPORT
----

===== Report Types Definition

After having defined your menus and actor, you can enter different field types definitions based on the standard field types or code field types, you can also use the LIST and SELECT commands
to customize these new types. 

*Syntax:*
----
TypeDefinition:  "TYPE" SimplName "IS" FieldType  [TypeList] "END" "TYPE"
----

*Example*
 	
[source,java]
----  
REPORT "List of the Lecturers"
  {
    package at.dms.apps.tb;
  }
  TYPE Name IS
    STRING (20,10,4)
    CONVERT UPPER
  END TYPE

  TYPE Lesson IS
    LONG (11)
    MINVAL 0
    MAXVAL 100
  END TYPE
  
  TYPE Answer IS
    CODE BOOL IS
    	"Yes"	= TRUE
    	"No"  = FALSE
    END CODE
  END TYPE
  
  TYPE Months IS
    ENUM("January", "February", "March", "April")
  END TYPE 
  
TYPE Lecturer IS
  STRING (8)
  CONVERT UPPER

  LIST "Lecturer" IS
    "Symbol" = Symbol : STRING (8)
    "Name" = Name : STRING (40)
    "Surname" = Surname : STRING (40)
    "Lesson" = Lesson : STRING (20)
  END LIST
...
END TYPE
    
  ...
END REPORT
----

===== Report Commands Definition

In this section you may want to define new commands, to do so, all you need is an already defined Actor from which you will call the command in order to execute an Action on the form. There are 3 ways to define this Action:
every command have an effective ray of action (VRField, VReport)

 * Calling a function with the CALL command
 * Calling a function outside of the report using the EXTERN command
 * Simply writing the body of the action using the  ACTION command, the parameters are optional and can be VRField or VReport. 
 
*Syntax*
[source,java]
----
cmdDef: "COMMAND" QualifiedName
        "ITEM" SimpleName
        commandBody
        "END" "COMMAND"

cmdBody: "CALL" SimpleName | "EXTERN" QualifiedName 
         |"ACTION"
         [(VField SimpleName | VBlock SimpleName | VForm SimpleName)]
         {KOPI_JAVA statements}
----


*Example*

Calling a local action :

[source,java]
----
COMMAND print
  ITEM  printActor
  CALL  printReport
END COMMAND
----

*Example*
Calling an external action : 

[source,java]
----
COMMAND doAction
  ITEM Save
  EXTERN com.company.vkopi.lib.report.Actions
END COMMAND
----

*Example*
Writing the action's body :

[source,java]
----
COMMAND PrintReport
  ITEM  PrintReport
  ACTION (VReport report){
    // KOPI_JAVA code
  }
END COMMAND
----

===== Report Insert Definition
This command allows you to actually define your types, menus, actors, commands in another file, and then insert it in any report you want, thus avoiding rewriting recurrent definitions. You still can add definition before or after using the INSERT command.

*Syntax* 
[source,java]
----
Insert Definition : "INSERT" String filePath
----

*Example*
[source,java]
----
REPORT "List of Students"
 
  {
  package at.dms.app.application
  }
   
  INSERT "ReportDefault.vr"
  
  TYPE Lesson IS
    LONG(11)
  END TYPE

...  
END REPORT    
----

===== Report Commands Declaration

After the Definitions section of the report, you have to enter the BEGIN keyword in order to begin the report declaration part where you can optionally call report commands and triggers, and where you have to enter the report's fields.
Concerning the commands declaration, you can start by the REPORT COMMAND statement that add all the default report commands to your report that include printing and exporting the report.
Now you may call other commands using the COMMAND keyword followed by a defined command name from those who already exist or the ones you defined in the command definition section.

*Example*
[source,java]
----
COMMAND Sort
COMMAND PrintReport
----

You can also define your command when declaring it instead of defining it in the command definition section of the report, simply write the command definition like explained before, but with no name, in the command declaration section of the report.

*Example*
[source,java]
----
COMMAND
  ITEM actorName
  ACTION {
    KOPI_JAVA code  
  }
END COMMAND


COMMAND 
  ITEM actorName
  CALL localFunction
END COMMAND
----

===== Report Triggers Declaration

Report Triggers are special events that once switched on you can execute a set of actions defined by the following syntax :

*Syntax*
----
ReportTrigger  :    ReportEventList TriggerAction
ReportEventList:  ReportEvent [,FormEvent]*
----

Kopi actually defines 2 report Triggers or report Events : 

 * PREREPORT  : executed before the report is displayed.
 * POSTREPORT : executed after the report is closed.

*Example*
[source, java]
----
REPORT "Orders Report"
{
  package at.dms.app.application
}

INSERT "ReportDefault.vr"

BEGIN

  REPORT COMMAND

  PREREPORT{
    System.out.println("This message is displayed before the report appears");
  }
  ...
END REPORT
----

===== Report Fields Declaration 

As you already know, a dynamic report is based on field that will be shown as report columns, in this section you have to write at least on field definition or more following 
the definition and the structure we saw in the previous chapter.

===== Report Context Footer:

This section should follow the report fields declaration and have to be placed between curly braces, here you may define all the functions, data, classes you need in your report, written in KOPI_JAVA code.
But most important, you need to define the constructor of you report, this constructor will be responsible of filling the report's lines or rows.
All you need to do is importing your data (a List or vector parameter, database query cursors ...) , declaring a row in the report then add the Add(); statement to add the row to the report.
The constructor may have different parameters but a DBContextHandler object is compulsory (a form most of the time)
For example here is the constructor of a dynamic report named UserList with 3 fields (FirstName, LastName, Age).We will retrieve data from the User table on the database. 

*Example*
[source,java]
----
void UserList(DBContextHandler context) throws VException {
  super(context);
  #protected () {
    try {
      #cursor (){
        SELECT int U.age,
               String U.firstname,
               String U.lastname,
    
        FROM    User U
  
      } cursor;
    
      cursor.open();
    
      while (cursor.next()) 
      {
        FirstName = cursor.U.firstname;
        LastName  = cursor.U.lastname;
        Age       = cursor.U.age;
        add();
      }
      cursor.close();
   } catch (DBNoRowException) {
        System.out.println("DBNoRowException");
      }
  }
}

----

==== Calling reports

A report is always called from a form, if the caller form extends from the VDictionaryForm class you have to do the following steps :

 * Change VDictionaryForm to VReportSelectionForm
 * Add the CreateReport command to the caller form
 * Implement the createReport abstract method :

*Example*
[source,java]
----
 protected VReport createReport() throws VException{
   return new USerList(this);
 }
----

Otherwise you can create a normal form or block command that executes the following code :

[source,java]
----
   WindowController.getWindowController().doNotModal(new UserList(this));
----


